// -*- coding: utf-8; mode: groovy -*-

buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    // classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"

  }

}
plugins {
  id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'java'

apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'clean', 'build'

// ======== deployment options (dispatched on group name) ========
group =   'org.xmlsh.aws'
version = "0.23-SNAPSHOT"

sourceCompatibility = targetCompatibility = 1.8

ext {
  artifactId = 'gradle-aws-plugin'
  defaultEncoding = 'UTF-8'
}

// ======== create source and javadoc bundles ========

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

javadoc {
  failOnError = false
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

eclipse {
   classpath {
     downloadJavadoc = true
     downloadSources = true
   }
}

configurations {
  deployerJars
}

// ======== library versions ========
ext {
  lombokVersion = '1.16.2'
  awsJavaSdkVersion = '1.10.+'
  groovyVersion = '2.3.7'
  junitVersion = '4.+'
  hamcrestVersion = '1.3'
  mockitoCoreVersion = '1.9.5'
  guavaVersion = '18.0'
}

repositories {
  jcenter()
  mavenLocal()
}

dependencies {
  compile gradleApi()
  compile "com.google.guava:guava:$guavaVersion"
  compile "commons-io:commons-io:1.4"
  compile "org.projectlombok:lombok:$lombokVersion"

  compile "com.amazonaws:aws-java-sdk-s3:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-ec2:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-rds:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-route53:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-elasticloadbalancing:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-elasticbeanstalk:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-cloudformation:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-lambda:$awsJavaSdkVersion"
  compile "com.amazonaws:aws-java-sdk-iam:$awsJavaSdkVersion"

  // tests
  testCompile "junit:junit:$junitVersion"
  testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
  testCompile "org.mockito:mockito-core:$mockitoCoreVersion"

  deployerJars "org.springframework.build:aws-maven:4.7.0.RELEASE"
}

// ======== deploy artifacts ========
// Allows forks of the plugin to define their own deployment mechanisms
// in separate files named according to their maven group name
apply from: "deploy/${group}.gradle"

// ======== wrapper ========
task wrapper(type: Wrapper) {
  version '2.10'
}
// ======== License =======
license {
 // skipExistingHeaders false
 // ignoreFailures true
 //// exclude "**/.gitkeep"
 // include "src/**/*.groovy"
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.email =  "dlee@calldei.com"
  header file('copyright/HEADER')
  strictCheck true
  mapping {
    java = 'SLASHSTAR_STYLE'
  }
}

